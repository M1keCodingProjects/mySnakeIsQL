How the grammar syntax works:
RuleName : Rule
FinalRule :: combination of RegExp, Literals or DirectTokens
? = 0 or 1 times
() = group
| = either option
+ = 1 or more
* = 0 or more
--------------------------------
Query : Select From Where? ";"?
    Select : "SELECT" AttrList
        AttrList : Attr ("," Attr)*
            Attr :: "*" | IDENT

    From : "FROM" Table (("," Table) | Join)*
        Table :: IDENT
        Join : ("OUTER"? ("LEFT" | "FULL" | "RIGHT") | "INNER"? "NATURAL")? "JOIN" Table "ON" Predicate

    Where : "WHERE" Predicate
        Predicate : ComparisonExpr (LogicalOp ComparisonExpr)* | "(" Predicate ")"
            ComparisonExpr : MathExpr (CompareOp MathExpr)? | BetweenExpr
                MathExpr : Operand | (MathExpr MathOp)* MathExpr
                    Operand : Literal | Attr
                    MathOp :: "+" | "-" | "*" | "/" | "%"
                    Literal :: INT_LITERAL | STR_LITERAL | DATE_LITERAL
                
                CompareOp :: "==" | "!=" | "<>" | "<" | ">" | ">=" | "<="
                BetweenExpr : "BETWEEN" MathExpr "AND" MathExpr
            
            LogicalOp :: "AND" | "OR"